// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=outt, out=outinc);
    Mux16(a=outt, b=outinc, sel=inc, out=incorelse);
    Mux16(a=incorelse, b=in, sel=load, out=loadorincorelse);
    Mux16(a=loadorincorelse, b=false, sel=reset, out=outtmp);

    Register(in=outtmp, load=true, out=out, out=outt);
}
